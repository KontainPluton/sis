//=======================================================================
//        Gradle Project Build File
//
//  Licensed to the Apache Software Foundation (ASF) under one
//  or more contributor license agreements.  See the NOTICE file
//  distributed with this work for additional information
//  regarding copyright ownership.  The ASF licenses this file
//  to you under the Apache License, Version 2.0 (the
//  "License"); you may not use this file except in compliance
//  with the License.  You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing,
//  software distributed under the License is distributed on an
//  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
//  KIND, either express or implied.  See the License for the
//  specific language governing permissions and limitations
//  under the License.
//=======================================================================

plugins {
    id("org.apache.sis.java-conventions")
    antlr
}

val antlrVersion = "4.10.1"

// TODO: this entire source set configuration can be removed if 'antlr4' source directory is simply renamed to 'antlr'
sourceSets {
    main {
        antlr.setSrcDirs(listOf("src/main/antlr4/"))
    }
}

dependencies {
    antlr("org.antlr:antlr4:$antlrVersion")

    api(project(":core:sis-feature"))
    compileOnly(libs.geometry.jts)
    implementation("org.antlr:antlr4-runtime:$antlrVersion")
    testImplementation(project(":core:sis-utility"))
}

group = "org.apache.sis.core"
description = "Apache SIS CQL"

val testsJar by tasks.registering(Jar::class) {
    archiveClassifier.set("tests")
    from(sourceSets["test"].output)
}

(publishing.publications["maven"] as MavenPublication).artifact(testsJar)

// Force classes generated by Antlr to be assigned the given package.
tasks.generateGrammarSource {
    arguments.addAll(listOf("-package", "org.apache.sis.internal.cql"))
}
